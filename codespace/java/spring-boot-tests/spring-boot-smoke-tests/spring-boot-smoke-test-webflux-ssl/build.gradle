plugins {
	id "java"
	id "org.springframework.boot.conventions"
	id "org.springframework.boot.integration-test"
}

description = "Spring Boot WebFlux SSL smoke test"


configurations {
	app
}

dependencies {
	app project(path: ":spring-boot-project:spring-boot-dependencies", configuration: "mavenRepository")
	app project(path: ":spring-boot-project:spring-boot-parent", configuration: "mavenRepository")
	app project(path: ":spring-boot-project:spring-boot-tools:spring-boot-gradle-plugin", configuration: "mavenRepository")
	app project(path: ":spring-boot-project:spring-boot-starters:spring-boot-starter-webflux", configuration: "mavenRepository")

	testImplementation(enforcedPlatform(project(":spring-boot-project:spring-boot-parent")))
	testImplementation(project(":spring-boot-project:spring-boot-tools:spring-boot-test-support"))
	testImplementation(project(":spring-boot-project:spring-boot-starters:spring-boot-starter-test"))
	testImplementation("org.testcontainers:junit-jupiter")
	// For the WebClient in tests
	testImplementation(project(":spring-boot-project:spring-boot-starters:spring-boot-starter-webflux"))
}

task syncMavenRepository(type: Sync) {
	from configurations.app
	into "${buildDir}/int-test-maven-repository"
}

task syncAppGradleFiles(type: org.springframework.boot.build.SyncAppSource) {
	sourceDirectory = file("spring-boot-starter-webflux-tests-app")
	destinationDirectory = file("${buildDir}/spring-boot-starter-webflux-tests-app")
}

task syncAppSource(type: org.springframework.boot.build.SyncAppSource) {
	sourceDirectory = file("../spring-boot-smoke-test-webflux/src/main")
	destinationDirectory = file("${buildDir}/spring-boot-starter-webflux-tests-app/src/main")
}

task buildApp(type: GradleBuild) {
	dependsOn syncAppGradleFiles, syncAppSource, syncMavenRepository
	dir = "${buildDir}/spring-boot-starter-webflux-tests-app"
	startParameter.buildCacheEnabled = false
	tasks  = ["build"]
}

test {
	dependsOn buildApp
}
